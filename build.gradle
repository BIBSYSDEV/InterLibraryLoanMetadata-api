plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "org.owasp.dependencycheck" version "6.3.1"
}

group 'no.unit'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11  // source-code version and must be <= targetCompatibility
targetCompatibility = JavaVersion.VERSION_11 // bytecode target version

repositories {
    mavenCentral()
}

project.ext {
    jaxbTargetDir = file("src/main/java")
}

configurations {
    xsd2java
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.8'
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '3.10.0'
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.1'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-core', version: '1.12.68'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.8'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '3.0.1'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '3.0.2'
    implementation group: 'com.github.bibsysdev', name: 'core', version: '1.9.0'
    implementation group: 'com.github.bibsysdev', name: 'apigateway', version: '1.8.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.8.0'
    xsd2java group: 'com.sun.xml.bind', name: 'jaxb-xjc', version: '3.0.2'
    xsd2java group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '3.0.2'
    xsd2java group: 'javax.xml.bind', name: 'jaxb-api', version: '2.4.0-b180830.0359'
    xsd2java group: 'javax.activation', name:'javax.activation-api', version: '1.2.0'

    shadowJar {
        archiveClassifier.set('')
        zip64 true
    }
}

task xsd2java() {
    doLast {
        jaxbTargetDir.mkdirs()
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.xsd2java.asPath)
        ant.jaxbTargetDir = jaxbTargetDir
        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: 'no.nb.basebibliotek.generated',
                schema: 'src/main/xsd/basebibliotek.xsd'
        )
    }
}
compileJava.dependsOn xsd2java

test {
    useJUnitPlatform()
    failFast = true
    finalizedBy jacocoTestReport
}

// We don't want a jar, just a fatJar
jar.enabled = false
project.tasks.build.dependsOn project.tasks.shadowJar

pmd {
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
    toolVersion '8.41'
}

tasks.withType(Checkstyle) {
    exclude { 'no/nb/basebibliotek/generated/*' }
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco")
    }
}

jacoco {
    toolVersion = "0.8.5"
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {
    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude:  [
                    'no/nb/basebibliotek/generated/*'
            ])
        })
    }
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
        }
    }
}

